options
{
	LOOKAHEAD=2;
}
PARSER_BEGIN(MathParser)
/*
 * Copyright (C) 2013 The University of Texas at Austin
 *
 * This file is part of PerfExpert.
 *
 * PerfExpert is free software: you can redistribute it and/or modify it under
 * the terms of the GNU Lesser General Public License as published by the Free
 * Software Foundation, either version 3 of the License, or (at your option) any
 * later version.
 *
 * PerfExpert is distributed in the hope that it will be useful, but WITHOUT ANY
 * WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR
 * A PARTICULAR PURPOSE. See the GNU Lesser General Public License for more
 * details.
 *
 * You should have received a copy of the GNU Lesser General Public License
 * along with PerfExpert. If not, see <http://www.gnu.org/licenses/>.
 *
 * Author: Ashay Rane
 */

package edu.utexas.tacc.perfexpert.configuration.hpctoolkit.mathparser;

import java.util.Properties;

import edu.utexas.tacc.perfexpert.parsing.profiles.hpctoolkit.HPCToolkitProfile;

import org.apache.log4j.Logger;

public class MathParser
{
	public MathParser()
	{
		//java.io.StringReader sr = new java.io.StringReader(input);
		//java.io.Reader r = new java.io.BufferedReader(sr);
		this(new java.io.BufferedReader(new java.io.StringReader("")));
	}

	static Logger log = Logger.getLogger( MathParser.class );
	public double parse(String input, HPCToolkitProfile profile, Properties machineConfig) throws ParseException
	{
		java.io.StringReader sr = new java.io.StringReader(input);
		java.io.Reader r = new java.io.BufferedReader(sr);
		// MathParser parser = new MathParser(r);
		ReInit(r);
		return parseOneLine(profile, machineConfig);
	}

	private static double getSymbolValue(String token, HPCToolkitProfile profile, Properties machineConfig)
	{
		// Check in the profile (for perf counter values) and in the machine configuration (for machine constants like BR_lat)
		// If not present in both, log an error and return 0;

		String machineValue = machineConfig.getProperty(token);
		if (machineValue == null || machineValue.length() == 0)
		{
			// Check among perf counters
			Integer index = profile.getConstants().getPerfCounterTranslation().get(token);

			if (index == null)
			{
				// Before throwing an error, check for one last time if a metric exists with just the prefix
				int indexOfColon = token.indexOf(':');

				if (indexOfColon != -1)
				{
					index = profile.getConstants().getPerfCounterTranslation().get(token.substring(0, indexOfColon));
					if (index != null)
						return profile.getMetricBasedOnPEIndex(index);
				}

				log.error("Could not find definition for \u005c"" + token + "\u005c" while parsing LCPI metrics, defaulting to zero");
				return 0;
			}

			return profile.getMetricBasedOnPEIndex(index);
		}

		return Double.parseDouble(machineValue);
	}
}
PARSER_END(MathParser)
SKIP :
{
	" "
|	"\r"
|	"\t"
|	"\n"
}
TOKEN:
{
	< NUMBER: <SIMPLENUMBER> ("e" (<DIGIT>)+)? >
|	< SIMPLENUMBER: (<DIGIT>)+ ( "." (<DIGIT>)+ )? >
|	< DIGIT: ["0"-"9"] >
|	< LETTER: ["A"-"Z"]|["a"-"z"] >
|	< SYMBOL: ("_"|":"|<LETTER>|<DIGIT>)+ >
}
double parseOneLine(HPCToolkitProfile profile, Properties machineConfig):
{
	double a;
}
{
	a=expr(profile, machineConfig) <EOF>		{ return a; }
|	<EOF>			{ return 0; }
}
double expr(HPCToolkitProfile profile, Properties machineConfig):
{
	double a;
	double b;
}
{
	a=term(profile, machineConfig)
	(
		"+" b=expr(profile, machineConfig)	{ a += b; }
	|	"-" b=expr(profile, machineConfig)	{ a -= b; }
	)*
				{ return a; }
}
double term(HPCToolkitProfile profile, Properties machineConfig):
{
	double a;
	double b;
}
{
	a=unary(profile, machineConfig)
	(
		"*" b=term(profile, machineConfig)	{ a *= b; }
	|	"/" b=term(profile, machineConfig)	{ if (b != 0) a /= b; }
	)*
				{ return a; }
}
double unary(HPCToolkitProfile profile, Properties machineConfig):
{
	double a;
}
{
	"-" a=element(profile, machineConfig)		{ return -a; }
|	a=element(profile, machineConfig)		{ return a; }
}
double element(HPCToolkitProfile profile, Properties machineConfig):
{
	Token t;
	double a;
}
{
	t=<NUMBER>		{ return Double.parseDouble(t.toString()); }
|	t=<SYMBOL>		{ return getSymbolValue(t.toString(), profile, machineConfig); }
|	"(" a=expr(profile, machineConfig) ")"	{ return a; }
}
